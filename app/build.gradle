apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "pt.uninova.s4h.citizenhub"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 120221206
        versionName "0.13.2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            matchingFallbacks = ['release']
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development
        stable
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    productFlavors.all { flavor ->
        def propertiesFile = rootProject.file("env.${flavor.name}.properties")
        def properties = new Properties()

        properties.load(new FileInputStream(propertiesFile))

        manifestPlaceholders = [
                clientId      : properties["smart4HealthId"],
                clientSecret  : properties["smart4HealthSecret"],
                environment   : properties["smart4HealthEnvironment"],
                redirectScheme: properties["smart4HealthRedirectScheme"],
                debug         : properties["smart4HealthDebug"],
                platform      : properties["smart4HealthPlatform"]
        ]

        buildConfigField("String", "SMART_BEAR_API_KEY", "\"${properties["smartBearApiKey"]}\"")
        buildConfigField("String", "SMART_BEAR_URL", "\"${properties["smartBearUrl"]}\"")
        buildConfigField("String", "SMART4HEALTH_APP_URL", "\"${properties["smart4HealthAppUrl"]}\"")

        flavor.resValue "string", "smart4HealthAppUrl", "${properties["smart4HealthAppUrl"]}"

        if (flavor.name != "stable" && properties["skipAuth"] != null) {
            buildConfigField("Boolean", "SKIP_AUTH", properties["skipAuth"])
        } else {
            buildConfigField("Boolean", "SKIP_AUTH", "false")
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources.excludes.add("META-INF/DEPENDENCIES")
    }

    namespace 'pt.uninova.s4h.citizenhub'
}

dependencies {
    def androidx_activity_version = "1.6.1"
    def androidx_appcompat_version = "1.6.1"
    def androidx_cardview_version = "1.0.0"
    def androidx_concurrent_version = "1.1.0"
    def androidx_constraintlayout_version = "2.1.4"
    def androidx_coordinatorlayout_version = "1.2.0"
    def androidx_core_version = "1.9.0"
    def androidx_customview_version = "1.1.0"
    def androidx_drawerlayout_version = "1.1.1"
    def androidx_fragment_version = "1.5.5"
    def androidx_lifecycle_version = "2.5.1"
    def androidx_preference_version = "1.2.0"
    def androidx_recyclerview_version = "1.2.1"
    def androidx_transition_version = "1.4.1"
    def androidx_viewpager_version = "1.0.0"
    def androidx_work_version = "2.8.0"
    def com_android_tools_version = "2.0.2"
    def ca_uhn_hapi_fhir_hapifhir_version = "6.4.2"
    def ca_uhn_hapi_fhir_org_hl7_fhir_r4_version = "5.6.971"
    def care_data4life_hcsdkkmp_version = "1.16.0"
    def care_data4life_hcfhirhelpersdkkmp_version = "1.9.0"
    def com_github_PhilJay_version = "v3.1.0"
    def com_google_android_gms_version = "18.0.0"
    def com_google_android_material_version = "1.8.0"
    def com_google_guava_version = "31.1-android"
    def com_squareup_okhttp3_version = "4.10.0"
    def pt_uninova_s4h_materialcalendarview = "2.0.1"

    implementation(project(":data"))
    implementation(project(":persistence"))
    implementation(project(":util"))

    implementation "androidx.activity:activity:$androidx_activity_version"

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"

    implementation "androidx.cardview:cardview:$androidx_cardview_version"

    implementation "androidx.concurrent:concurrent-futures:$androidx_concurrent_version"

    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"

    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidx_coordinatorlayout_version"

    implementation "androidx.core:core:$androidx_core_version"

    implementation "androidx.customview:customview:$androidx_customview_version"

    implementation "androidx.drawerlayout:drawerlayout:$androidx_drawerlayout_version"

    implementation "androidx.fragment:fragment:$androidx_fragment_version"

    implementation "androidx.lifecycle:lifecycle-common:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-core:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$androidx_lifecycle_version"

    implementation "androidx.navigation:navigation-common:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-fragment:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-runtime:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui:$androidx_navigation_version"

    implementation "androidx.preference:preference:$androidx_preference_version"

    implementation "androidx.recyclerview:recyclerview:$androidx_recyclerview_version"

    implementation "androidx.transition:transition:$androidx_transition_version"

    implementation "androidx.viewpager:viewpager:$androidx_viewpager_version"

    implementation "androidx.work:work-runtime:$androidx_work_version"

    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:$ca_uhn_hapi_fhir_hapifhir_version"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$ca_uhn_hapi_fhir_hapifhir_version"
    implementation "ca.uhn.hapi.fhir:org.hl7.fhir.r4:$ca_uhn_hapi_fhir_org_hl7_fhir_r4_version"
    runtimeOnly "ca.uhn.hapi.fhir:hapi-fhir-android:$ca_uhn_hapi_fhir_hapifhir_version"

    implementation("care.data4life.hc-sdk-kmp:sdk-android:$care_data4life_hcsdkkmp_version") {
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'securestore-jvm'
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'crypto-jvm'
        exclude group: 'care.data4life.hc-sdk-kmp', module: 'auth-jvm'
        exclude group: 'care.data4life.hc-util-sdk-kmp', module: 'util-jvm'
    }

    implementation "care.data4life.hc-fhir-helper-sdk-kmp:fhir-helper-android:$care_data4life_hcfhirhelpersdkkmp_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$com_android_tools_version"

    implementation "com.github.PhilJay:MPAndroidChart:$com_github_PhilJay_version"

    implementation "com.google.android.gms:play-services-wearable:$com_google_android_gms_version"

    implementation "com.google.android.material:material:$com_google_android_material_version"

    implementation "com.google.guava:guava:$com_google_guava_version"

    implementation "com.squareup.okhttp3:okhttp:$com_squareup_okhttp3_version"

    implementation "pt.uninova.s4h:material-calendarview:$pt_uninova_s4h_materialcalendarview"
}